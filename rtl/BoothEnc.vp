/*************************************************************************
 ** From Perforce:
 **
 ** $Id: //Smart_design/ChipGen/FP-Gen/rtl/Booth2Enc.vp#7 $
 ** $DateTime: 2011/11/11 21:07:01 $
 ** $Change: 9724 $
 ** $Author: shacham $
 *************************************************************************/

/* *****************************************************************************
 * Description:
 * Booth TWO encoding for a multiplier.
 * 
 * Booth encoder produce the selet signals for partial products for computing (x+xinc)*(y+yinc)
 * 
 * Modified Booth 2 encoder
 * 
 * Y[2:0]     out  =  pp   +   S
 * 
 * 0 0 0      +0      100000  +0      
 * 0 0 1      +X      (10X)   +0
 * 0 1 0      +X      (10X)   +0
 * 0 1 1      +2X     (1X0)   +0
 * 1 0 0      -2X     (0~X1)  +1
 * 1 0 1      -X      (01~X)  +1
 * 1 1 0      -X      (01~X)  +1
 * 1 1 1      -0      011111  +1
 * 
 * NOTE: There is 1 to be augmented to the left of the partial products 
 *       excepf for the first one
 * 
 * 
 * 
 * Parameters:
 * * Width
 * 
 * Change bar:
 * -----------
 * Date           Author    Description
 * Oct 05, 2011   shacham   init version - ported Sameh's original code to 
 *                          genesis
 * ****************************************************************************/

/*******************************************************************************
 * PARAMETERIZATION
 * ****************************************************************************/
//; # Perl Libraries
//; use POSIX qw(floor ceil); # For floor and ceiling rounding math
//;
//; # module parameters
//; my $booth = parameter(Name=>'BoothType', Val=>2, Min=>1, Step=>1, Max=>4,
//;                       Doc=>"Booth type. Required parameter at instantiation.");
//; my $width = parameter(Name=>'Width', Val=>24, Min=>1, Step=>1,
//;                       Doc=>"Signal bit widths");
//; my $useInc = parameter(Name=>'UseInc', Val=>'',Val=>1, List=>[0 , 1],
//; 			   Doc=>"Pick '0' for A*B or pick '1' for (A+ainc)*(B+binc)");
//; my $invert_sel = parameter(Name=>'InvertSel', Val=>'YES', List=>['YES', 'NO'],
//;                       Doc=>"denotes if the select signals are inverted or not");
//; my $modified = parameter(Name=>'ModifiedBooth', Val=>1, List=>[0 , 1],
//;                       Doc=>"Use Modified Booth Algorithm");
//;
//; # Width of y is possibly augmented by 1 bit at LSB and $booth-($width % $booth) at MSB
//; my $y_width = $width + ($useInc | $modified) + (($width%$booth || $modified)? $booth:0) - ( $width % $booth);
//;
//; # Number of partial product is rounded up from width/booth for booth and from width+1/booth for modified booth
//; my $pp_count = ceil(($width + $modified) / $booth);
//;
//; # the number of select signals. the uppermost signal is sign in case of modified booth and spare signal in case of modified booth
//; my $sel_width = 2**($booth-$modified)+$modified;

module `mname` (
    input logic [`$width-1`:0]  x,
    input logic [`$y_width-1`:0] extended_y,
      //;   if ($booth-$modified>=2) {
    output logic [`$width+1`:0] x3,
      //;   }
      //;   if ($booth-$modified>=3) {
    output logic [`$width+2`:0] x5, x7,
      //;   }
    output logic [`$pp_count*$sel_width-1`:0] sel_x
		);

  /* synopsys dc_tcl_script_begin
   set_ungroup [current_design] true
   set_flatten true -effort high -phase true -design [current_design]
  */

   //; if ($booth-$modified>=2) {
   //;   my $HM3_obj = generate('HardMultiple3', 'HM3', Width=>$width);
   `$HM3_obj->instantiate`  (.x(x), .x3(x3));
   //; }
   
   //; if ($booth-$modified>=3) {
   //;   my $HM5_obj = generate('HardMultiple5', 'HM5', Width=>$width);
   `$HM5_obj->instantiate` ( .x(x), .x5(x5));

   //;   my $HM7_obj = generate('HardMultiple7', 'HM7', Width=>$width);
   `$HM7_obj->instantiate` ( .x(x), .x7(x7));
   //; }

   //; for(my $i=0; $i < $pp_count; $i++){
   //;   my $BoothSel_obj = generate('BoothSel', "BoothEnc_u${i}_Booth_sel_0", BoothType=>$booth, 
   //;                 UseInc => (!$modified && $useInc && !$i)?1:0, ModifiedBooth => $modified, InvertSel => $invert_sel);
   `$BoothSel_obj->instantiate` ( 
   //;   if ( $modified ) {
                                  .y ( extended_y[`($i+1)*$booth`:`$i*$booth`] ) ,
   //;   } elsif ( $useInc ) {
   //;     if ( $i ) {
                                  .y ( extended_y[`($i+1)*$booth`:`$i*$booth+1`] ) ,
   //;     } else  {
                                  .y ( extended_y[`$booth`:0] ) ,
   //;     }
   //;   } else {
                                  .y ( extended_y[`($i+1)*$booth-1`:`$i*$booth`] ) ,
   //;   }
                                  .sel_x ( sel_x[`($i+1)*$sel_width-1`:`$i*$sel_width`] ) );
   //; }

endmodule

