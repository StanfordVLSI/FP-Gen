/*************************************************************************
 ** From Perforce:
 **
 ** $Id$
 ** $DateTime$
 ** $Change$
 ** $Author$
 *************************************************************************/

/* *****************************************************************************
 * Description:
 * Booth encoding for a multiplier.
 * 
 * Booth encoder produce the partial products for computing (x+xinc)*(y+yinc)
 * 
 * xinc yinc  result
 * 0    0     xy
 * 0    1     xy+x     (through y[-1]=yinc of booth encoding)
 * 1    0     xy+y     (through adding y as pp[0])
 * 1    1     xy+x+y+1 (through both methods and 1 is added to pp[2] to S[0])
 * 
 * Sign encoding:
 * --------------
 * SSSSSS.....S = -1 + ~S
 * 
 * S0 S0 S0 S0 S0 S0 S0 S0 S0 S0                              ~S0                      ~S0  S0  S0
 *                                                          0  -1
 * S1 S1 S1 S1 S1 S1 S1 S1                 =            ~S1               =          1 ~S1
 *                                                    0  -1
 * S2 S2 S2 S2 S2 S2                              ~S2                          1 ~S2
 *                                              0  -1
 * 
 * 
 * Final tree:
 * -----------                  __
 *                              S0 S0 S0 *  *  *  *  *  *  *  *  0  0
 *                              __
 *                           1  S1 *  *  *  *  *  *  *  *  0  S0
 *                        __
 *                     1  S2 *  *  *  *  *  *  *  *  0  S1
 *                  __
 *               1  S3 *  *  *  *  *  *  *  *  0  S2
 * 
 *   __
 * 1 Sn *  *  *  *  *  *  *  *  0  S(n-1)
 * 
 *                           Sn = 0 (because y is extended)    
 * 
 * 
 * Parameters:
 * * Width, BoothType
 * 
 * Exported Parameters:
 * * PPWidth, PPCount
 * 
 * Change bar:
 * -----------
 * Date           Author    Description
 * Oct 04, 2011   shacham   init version - ported Sameh's original code to 
 *                          genesis + a little restructuring
 * Nov 09, 2011   shacham   Added basic functionality for booth 4
 * 
 * ****************************************************************************/


/*******************************************************************************
 * PARAMETERIZATION
 * ****************************************************************************/
//; # Perl Libraries
//; use POSIX qw(ceil); # For ceiling rounding math
//;
//; # get some values from the top
//; my $verif_mode = $self->get_top_param('VERIF_MODE'); # Reads this parameter from the top level
//; my $synth_mode = $self->get_top_param('SYNTH_MODE'); # Reads this parameter from the top level
//;
//;
//; # Interface parameter
//; my $useInc = parameter(Name=>'UseInc', Val=>'', List=>['YES', 'NO'],
//; 			   Doc=>"Pick 'NO' for A*B or pick 'YES' for (A+ainc)*(B+binc)");
//; $useInc = ($useInc =~ /YES/)?1:0;
//;
//; # module parameters
//; my $width = parameter(Name=>'Width', Val=>53, Min=>1, Step=>1,
//;                       Doc=>"Signal bit widths");
//; my $booth = parameter(Name=>'BoothType', Val=>2, Min=>1, Step=>1, Max=>4,
//;                       Doc=>"Booth type. (1 for no booth, 2 is default, 3 and 4 may sometimes be useful when Width is very large)");
//; my $modified = parameter(Name=>'ModifiedBooth', Val=> ($booth==1)?0:1, List=>[0 , 1],
//;                       Doc=>"Use Modified Booth Algorithm");
//; my $invert_sel = ($booth==2 || $modified==0)? 'YES':'NO';
//; my $invert_x = ($booth==1)? 'YES':'NO';
//; my $pipeline_depth = parameter(Name=>'PipelineDepth', Val=>0, Min=>0 , Step=>1, Doc=>"Pipeline depth");
//;
//; # Width of y is possibly augmented by 1 bit at LSB and $booth-($width % $booth) at MSB
//; my $y_width = $width + ($useInc | $modified) + (($width%$booth || $modified)? $booth:0) - ( $width % $booth);
//;
//; # Number of partial product is rounded up from width/booth for booth and from width+1/booth for modified booth
//; my $pp_count = ceil(($width + $modified) / $booth);
//;
//; # Width of partial product is width+booth and is augmented by 2booth for modified for sign extension and 2 booth for unmodified when there is increment signal
//; my $pp_width = $modified? $width+3*$booth : $width + 2*$booth + $useInc * $booth;
//;
//; # the number of select signals. the uppermost signal is sign in case of modified booth and spare signal in case of modified booth
//; my $sel_width = 2**($booth-$modified)+$modified;
//;
//; # Inform the world about the partial products properties
//; parameter(Name=>'PPWidth', Val=>$pp_width, Force=>1, 
//;           Doc=>"This is the calculated partial product width for Booth=$booth and Width=$width");
//; parameter(Name=>'PPCount', Val=>$pp_count+$useInc, Force=>1, 
//;           Doc=>"This is the calculated number of partial products for Booth=$booth and Width=$width. ".
//;                "Note that it includes a +1 to account for xinc and yinc");


module `mname`(
   //; if ($verif_mode =~ /ON/){
    input logic dumptolog,
   //; }
    input [`$width-1`:0] x_in, y_in,
   //; if ($useInc){
    input 		 xinc_in, yinc_in,
   //; }
   //; if ($pipeline_depth>0) {
    input logic  clk , reset, valid_in, stall,
    output logic  valid_out,
   //; }
    output [`$pp_width-1`:0] pp [0:`$pp_count+$useInc-1`] // `$pp_count`+ `$useInc` to account for xinc and yinc
		);

    logic [`$width-1`:0] x, y;
    logic xinc, yinc;
   //; if (!$useInc){
    logic xinc_in, yinc_in;
    assign {xinc_in, yinc_in} = 0; 
   //; }
   //; if ($pipeline_depth>0) {
   //;   my $pipe_XY = generate(  'FF_pipeline',  'pipe_XY',  
   //;                             Width=> 2*$width + 2 , PipelineDepth => $pipeline_depth, Retime=>'YES');
  `$pipe_XY->instantiate()` ( .in({x_in, y_in, xinc_in, yinc_in}) ,
                              .valid_in(valid_in), .clk(clk) , .stall(stall), .reset(reset), 
                              .out({x,y,xinc, yinc}), .valid_out(valid_out) );

   //; } else {
    assign {x,y,xinc, yinc} = {x_in, y_in, xinc_in, yinc_in};
   //; }

   logic [`$y_width`-1:0] extended_y;
   assign extended_y = {
   //; if ($width%$booth || $modified) {
                                      {(`$booth - ( $width % $booth) `) {1'b0} },y_in
   //; } else {
                                      y_in
   //; }
   //; if ($useInc) {
                                      , yinc_in
   //; } elsif ($modified) {
                                      , 1'b0
   //; }
                                              };

   logic [`$pp_count*$sel_width-1`:0] sel_x_intermediate, sel_x; //Encoded select signals
   //; if ($pipeline_depth>0) {
   logic valid_sel_x;
   //;   my $pipe_SelX = generate(  'FF_pipeline',  'pipe_SelX',  
   //;                             Width=> $pp_count*$sel_width , PipelineDepth => $pipeline_depth, Retime=>'YES');
  `$pipe_SelX->instantiate()` ( .in(sel_x_intermediate) ,
                              .valid_in(valid_in), .clk(clk) , .stall(stall), .reset(reset), 
                              .out(sel_x), .valid_out(valid_sel_x) );

   //; } else {
   assign sel_x = sel_x_intermediate;
   //; }

   //; if ($booth-$modified>=2) {
   logic [`$width+1`:0] x3, x3_intermediate;
   //;   if ($pipeline_depth>0) {
   logic valid_x3;
   //;     my $pipe_X3 = generate(  'FF_pipeline',  'pipe_X3',  
   //;                             Width=> $width + 2 , PipelineDepth => $pipeline_depth, Retime=>'YES');
  `$pipe_X3->instantiate()` ( .in(x3_intermediate) ,
                              .valid_in(valid_in), .clk(clk) , .stall(stall), .reset(reset), 
                              .out(x3), .valid_out(valid_x3) );

   //;   } else {
    assign x3 = x3_intermediate;
   //;   }
   //; }
   //; if ($booth-$modified>=3) {
   logic [`$width+2`:0] x5, x7, x5_intermediate, x7_intermediate;
   //;   if ($pipeline_depth>0) {
   logic valid_x5x7;
   //;      my $pipe_X5X7 = generate(  'FF_pipeline',  'pipe_X5X7',  
   //;                             Width=> 2*$width + 6 , PipelineDepth => $pipeline_depth, Retime=>'YES');
  `$pipe_X5X7->instantiate()` ( .in({x5_intermediate, x7_intermediate}) ,
                              .valid_in(valid_in), .clk(clk) , .stall(stall), .reset(reset), 
                              .out({x5, x7}), .valid_out(valid_x5x7) );

   //;   } else {
    assign {x5, x7} = {x5_intermediate, x7_intermediate};
   //;   }
   //; }
 

   //; my $BoothEnc_obj = generate('BoothEnc', "BE", BoothType=>$booth, Width=>$width, UseInc => $useInc, 
   //;                                         , ModifiedBooth => $modified, InvertSel => $invert_sel);
   `$BoothEnc_obj->instantiate`
     (.x(x_in), .extended_y(extended_y),
   //; if ($booth-$modified>=2) {
      .x3(x3_intermediate),
   //; }
   //; if ($booth-$modified>=3) {
      .x5(x5_intermediate), .x7(x7_intermediate),
   //; }	     
      .sel_x(sel_x_intermediate));


   logic [`$width+2*($booth-$modified)-1`:0] x_padded;
   //; if ($booth-$modified>=1){
   assign x_padded = {`$booth-$modified`'b0, x , `$booth-$modified`'b0};
   //; } else {
   assign x_padded = x;
   //; }

   //; if ($booth-$modified>=2){
   logic [`$width+2*($booth-$modified)-3`:0] x3_padded;
   //;   if ($booth-$modified>=3){
   assign x3_padded = {`$booth-$modified-2`'b0, x3 , `$booth-$modified-2`'b0};
   //;   } else {
   assign x3_padded = x3;
   //;   }
   //; }

   logic [`$width+$booth-1`:0] initial_pp [0:`$pp_count-1`] ;
   //; if ( $modified ) {
   logic [`$pp_count-1`:0] S;  // S[`$pp_count-1`] is guaranteed to be 0 because of the sign extension of 0 in extended y
   //;   if ($booth==2) {
   logic [`$width+2*($booth-$modified)-1`:0] xn [0:`$pp_count-1`];
   //;   }
   //;   for(my $i=0; $i < $pp_count; $i++){
   //;     if ($booth==2) {
   //;       my $BoothCell_obj = generate('Booth2SCell', "BoothEnc_u${i}_cell_0", InvertSel => $invert_sel);
   `$BoothCell_obj->instantiate` (.sel_x(sel_x[`($i+1)*$sel_width-1`:`$i*$sel_width`]), .S(S[`$i`]), .xn0(xn[`$i`][0]) );
   //;     } else {
   assign S[`$i`] = sel_x[`($i+1)*$sel_width-1`];
   //;     }
   assign initial_pp[`$i`][`$width+$booth-1`] = ~S[`$i`];
   //;   }
   //; }

   //; for(my $i=0; $i < $pp_count; $i++){
   //;   for(my $j=$booth-$modified; $j < $width+2*($booth-$modified); $j++){
   //;     my $BoothCellIdx = ($booth==2 && $modified)? $j : $j - ($booth-$modified);
   //;     my $BoothCell_obj = generate('BoothCell', "BoothEnc_u${i}_cell_$BoothCellIdx", BoothType=>$booth, 
   //;            InvertSel => $invert_sel, InvertX => $invert_x, ModifiedBooth => $modified , UseInc => ($i==0)?$useInc:0 );
   `$BoothCell_obj->instantiate` 
     (.sel_x(sel_x[`($i+1)*$sel_width-1`:`$i*$sel_width`]), .sel_x_bar( ~ sel_x[`($i+1)*$sel_width-1`:`$i*$sel_width`]),
      .x(  {
   //;     if ($booth-$modified>=3) {
            x_padded[`$j-3`], x7[`$j-3`], x3_padded[`$j-3`] , x5[`$j-3`],
   //;     }
   //;     if ($booth-$modified>=2) {
            x_padded[`$j-2`], x3_padded[`$j-2`],
   //;     }
           x_padded[`$j-1`], x_padded[`$j`] } ),
      .x_bar(  ~ {
   //;     if ($booth-$modified>=3) {
            x_padded[`$j-3`], x7[`$j-3`], x3_padded[`$j-3`] , x5[`$j-3`],
   //;     }
   //;     if ($booth-$modified>=2) {
            x_padded[`$j-2`], x3_padded[`$j-2`],
   //;     }
           x_padded[`$j-1`], x_padded[`$j`] } ),
   //;     if ($booth==2 && $modified) {
           .xn1(xn[`$i`][`$j-1`]), .xn0(xn[`$i`][`$j`]),
   //;     }
           .pp(initial_pp[`$i`][`$j-$booth+$modified`]));
   //;   } #j loop
   //; } #i loop
   
   // Finally, append the sign extenssions to the partial products
   //; if ($modified) { 
   //;   if ($useInc){
   assign pp[0] = xinc ? { {(`$booth`){1'b0}}, y, {(2*`$booth`){1'b0}} } : 0;
   assign pp[1] = {initial_pp[0][`$width+$booth-1`], {(`$booth`){S[0]}}, initial_pp[0][`$width+$booth-2`:0], {(`$booth`){1'b0}}};
   assign pp[2] = {1'b0, {(`$booth-1`){1'b1}}, initial_pp[1], {(`$booth-2`){1'b0}}, S[0] & xinc & yinc , S[0] ^ (xinc & yinc) };  //correction 1 is added for (x+1)(y+1) 
   //;     for(my $i=3; $i < $pp_count+1; $i++) {
   assign pp[`$i`] = {1'b0, {(`$booth-1`){1'b1}}, initial_pp[`$i-1`], {(`$booth-1`){1'b0}},S[`$i-2`]};
   //;     }
   //;   } else { # i.e., not $useInc
   assign pp[0] = {initial_pp[0][`$width+$booth-1`], {(`$booth`){S[0]}}, initial_pp[0][`$width+$booth-2`:0], {(`$booth`){1'b0}}};
   //;     for(my $i=1; $i < $pp_count; $i++) {
   assign pp[`$i`] = {1'b0, {(`$booth-1`){1'b1}}, initial_pp[`$i`], {(`$booth-1`){1'b0}},S[`$i-1`]};
   //;     }
   //;   }
   //; } elsif ($useInc) {
   assign pp[0] = xinc ? { y, {(2*`$booth`){1'b0}} } : 0;
   assign pp[1] = {initial_pp[0], {(`$booth`){1'b0}}};
   assign pp[2] = {initial_pp[1], {(`$booth-1`){1'b0}}, (xinc & yinc) };  //correction 1 is added for (x+1)(y+1) 
   //;     for(my $i=3; $i < $pp_count+1; $i++) {
   assign pp[`$i`] = { initial_pp[`$i-1`] , {(`$booth`){1'b0}} };
   //;     }

   //; } else {
   //;     for(my $i=0; $i < $pp_count; $i++) {
   assign pp[`$i`] = {{(`$booth`){1'b0}}, initial_pp[`$i`] };
   //;     }
   //; } 

endmodule: `mname`

