/*************************************************************************
 ** From Perforce:
 **
 ** $Id$
 ** $DateTime$
 ** $Change$
 ** $Author$
 *************************************************************************/

/* *****************************************************************************
 * Description:
 * This is a ArrayColumn that process a vertical slice in multiplication tree 
 * using a linear array architecture
 *
 * Parameters:
 * * ColumnHight, ColumnPosition, MinRow, BoothType, CarryInFlag, LayoutShape
 * 
 * Note that if:
 * CarryInFlag == Normal ==> ci_w=N-3 , co_w=N-3
 * CarryInFlag == OneMissing ==> ci_w=N-4 , co_w=N-3
 * CarryInFlag == OneExtra ==> ci_w=N-2 , co_w=N-2
 * CarryInFlag == TwoExtra ==> ci_w=N-1 , co_w=N-2
 *
 * Inputs:
 * * Given that N=ColumnHight
 * * [N-1:0] pp                 
 * * [ci_w-1:0] ci
 *  
 * Outputs:
 * * sum
 * * carry
 * * [co_w-1:0] co
 *
 * Change bar:
 * -----------
 * Date           Author    Description
 * Oct 21, 2011   Sameh     initial version
 * 
 * ****************************************************************************/

/*******************************************************************************
 * PARAMETERIZATION
 * ****************************************************************************/
//; # Perl Libraries
//; # use POSIX qw(floor ceil); # For floor and ceiling rounding math
//;
//; # get some values from the top
//; my $verif_mode = $self->get_top_param('VERIF_MODE'); # Reads this parameter from the top level
//; my $synth_mode = $self->get_top_param('SYNTH_MODE'); # Reads this parameter from the top level
//;
//; # module parameters

//; my $col_ht = parameter(Name=>'ColumnHight', Val=>2, Min=>2, Step=>1,
//;                          Doc=>"Number of partial products in relevant column to sum");
//; my $col = parameter(Name=>'ColumnPosition', Val=>0, Min=>0, Step=>1,
//;                          Doc=>"The column number");
//; my $min_row = parameter(Name=>'MinRow', Val=>0, Min=>0, Step=>1,
//;                          Doc=>"The starting row position");
//; my $booth = parameter(Name=>'BoothType', Val=>1, Min=>1, Max=>4, Step=>1,
//;                       Doc=>"What booth was used to create the partial products? ".
//;                            "(This determines the slope of the partial products ".
//;                            "paralleloid)");
//; my $ci_flag = parameter(Name=>'CarryInFlag', Val=>'Normal', List=>['Normal','OneMissing','OneExtra','TwoExtra'],
//;                      Doc=>"Is there one less carry in for this column? or maybe one or two more? (Normal is ColumnHight-3)");
//; my $layout_shape = parameter(Name=>'LayoutShape',Val=>'Square', List=>['Square','Parallelogram'],
//;                              Doc=>"Hints for layout positioning (Supported layouts: Square, Parallelogram)");
//;
//; my $ci_w = 0; my $co_w = 0; 
//; $ci_w = $col_ht-4 if $ci_flag =~ /OneMissing/;
//; $ci_w = $col_ht-3 if $ci_flag =~ /Normal/;
//; $ci_w = $col_ht-2 if $ci_flag =~ /OneExtra/;
//; $ci_w = $col_ht-1 if $ci_flag =~ /TwoExtra/;
//; $ci_w = 0 if ($ci_w < 0); # can't have negative width
//;
//; $co_w = $col_ht-3 if $ci_flag =~ /OneMissing/;
//; $co_w = $col_ht-3 if $ci_flag =~ /Normal/;
//; $co_w = $col_ht-2 if $ci_flag =~ /OneExtra/;
//; $co_w = $col_ht-2 if $ci_flag =~ /TwoExtra/;
//; $co_w = 0 if ($co_w < 0); # can't have negative width
//;
//; # print STDERR "DEBUG: hight=$col_ht, col=$col, min_row=$min_row, booth=$booth, ci_w=$ci_w, co_w=$co_w \n";

module `mname()` (
    input logic [`$col_ht-1`:0] pp,
//; if($ci_w){
    input logic  [`$ci_w-1`:0] ci,
//; }
//;if($co_w){
    output logic [`$co_w-1`:0] co,
//;}
    output logic sum, carry
   );

  /* synopsys dc_tcl_script_begin
   set_ungroup [current_design] true
   set_flatten true -effort high -phase true -design [current_design]
  */

   logic [`$co_w`:0] inter_sum;
   logic [`$co_w`:0] inter_co;

   //; # local variables for index etc.
   //; my $row = $min_row;
   //; my $Y = $col;
   //; my $pp_idx = 0; my $sum_idx = 0;
   //; my $ci_idx = 0; my $co_idx = 0;

   //; ############## First Cell ##############
   // First CSA (fully populated with PPs)
   //; # Determine the physical location for the CSA/HA cell
   //; $Y = $col - $row * $booth if $layout_shape =~ /Square/;
   //; $Y = $col if $layout_shape =~ /Parallelogram/;
   //; if ($col_ht==2){
   //;   if ($ci_w){
   //;   my $CSA_obj = generate('CSA', "csa_${row}_${Y}");
   // two partial products and one ci
   `$CSA_obj->instantiate()` ( .a(pp[`$pp_idx++`]), .b(pp[`$pp_idx++`]), .ci(ci[`$ci_idx++`]), 
			       .s(inter_sum[`$sum_idx`]), .co(inter_co[`$co_idx++`]) );
   //;   }else{
   //;   my $CSA_obj = generate('HA', "csa_${row}_${Y}");
   `$CSA_obj->instantiate()` ( .a(pp[`$pp_idx++`]), .b(pp[`$pp_idx++`]), 
			       .s(inter_sum[`$sum_idx`]), .co(inter_co[`$co_idx++`]) );
   //;   }
   //; }
   //; # else col_ht>2
   //;  else { 
   //;   my $CSA_obj = generate('CSA', "csa_${row}_${Y}");
   // No inter_sum or carry_in to the first CSA so use all inputs for pp
   `$CSA_obj->instantiate()` ( .a(pp[`$pp_idx++`]), .b(pp[`$pp_idx++`]), .ci(pp[`$pp_idx++`]), 
			       .s(inter_sum[`$sum_idx`]), .co(inter_co[`$co_idx++`]) );
   //; }
   //; $row++;

   
   //; ############## Column Body ##############
   //; while ($pp_idx<$col_ht && $ci_idx<$ci_w){
   //;   # Determine the physical location for the CSA/HA cell
   //;   $Y = $col - $row * $booth if $layout_shape =~ /Square/;
   //;   $Y = $col if $layout_shape =~ /Parallelogram/;
   //;   my $CSA_obj = generate('CSA', "csa_${row}_${Y}");
   // Regular case: pp, inter_sum and ci
   `$CSA_obj->instantiate()` ( .a(pp[`$pp_idx++`]), .b(inter_sum[`$sum_idx++`]), .ci(ci[`$ci_idx++`]), 
			       .s(inter_sum[`$sum_idx`]), .co(inter_co[`$co_idx++`]) );
   
   //;   $row++;
   //; } # End of "while ($pp_idx < $col_ht-1){..."

   //; ############## FINAL CELLS ##############
   //; # Determine the physical location for the CSA/HA cell
   //; $Y = $col - $row * $booth if $layout_shape =~ /Square/;
   //; $Y = $col if $layout_shape =~ /Parallelogram/;

   //; ##### One CI Missing
   //; if ($pp_idx<$col_ht && $ci_idx==$ci_w){
   //;   my $CSA_obj = generate('HA', "csa_${row}_${Y}");
   // Last PP but One missing Ci 
   `$CSA_obj->instantiate()` ( .a(pp[`$pp_idx++`]), .b(inter_sum[`$sum_idx++`]),
			       .s(inter_sum[`$sum_idx`]), .co(inter_co[`$co_idx++`]) );
   //; }
   //;
   //; ##### OneExtra Ci
   //; if ($pp_idx==$col_ht && $ci_idx==$ci_w-1){
   //;   my $CSA_obj = generate('HA', "csa_${row}_${Y}");
   // This column has one extra ci's
   `$CSA_obj->instantiate()` ( .b(ci[`$ci_idx++`]), .a(inter_sum[`$sum_idx++`]),
			       .s(inter_sum[`$sum_idx`]), .co(inter_co[`$co_idx++`]) );
   //; }
   //;
   //; ##### TwoExtra Ci
   //; if ($pp_idx==$col_ht && $ci_idx==$ci_w-2){
   //;   my $CSA_obj = generate('CSA', "csa_${row}_${Y}");
   // This column has two extra ci's
   `$CSA_obj->instantiate()` ( .a(ci[`$ci_idx++`]), .b(inter_sum[`$sum_idx++`]), .ci(ci[`$ci_idx++`]), 
			       .s(inter_sum[`$sum_idx`]), .co(inter_co[`$co_idx++`]) );
   //; }
   

   //; $self->error("How come ci_idx($ci_idx)!=ci_w($ci_w) or pp_idx($pp_idx)!=col_ht($col_ht) ".
   //;		    "or sum_idx($sum_idx)!=inter_sum_w($co_w) or co_idx($co_idx)!=co_w+1($co_w+1)??") 
   //; 		   unless $ci_idx==$ci_w && $pp_idx==$col_ht && $sum_idx==$co_w && $co_idx==$co_w+1;
   
   assign sum = inter_sum[`$co_w`];
   assign carry = inter_co[`$co_w`];
   //;if($co_w){
   assign co = inter_co[`$co_w-1`:0];
   //;}

endmodule

